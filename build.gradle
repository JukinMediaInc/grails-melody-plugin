buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.0" // dep for the artifactory plugin
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
    id "com.jfrog.bintray" version "1.5"
}

version "1.68.1.jk1"
group "org.grails.plugins"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "com.jfrog.artifactory" // artifactory plugin
apply plugin: 'maven-publish'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

// Define the artifact to publish
publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}
                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
            artifactId project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact source: "${sourceSets.main.output.classesDir}/META-INF/grails-plugin.xml",
                    classifier: "plugin",
                    extension: 'xml'
        }
    }
    repositories {
    }
}

// Artifactory config block
artifactory {
    contextUrl = artifactory_contextUrl
    publish {
        contextUrl = artifactory_publish_contextUrl
        repository {
            repoKey = artifactory_repository_repoKey
            username = artifactory_repository_username
            password = artifactory_repository_password
        }
    }
    resolve {
        repoKey = artifactory_resolve_repoKey
    }
}

artifactoryPublish {
    publications(publishing.publications.maven)
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.grails:grails-plugin-testing"

    console "org.grails:grails-console"

    //plugin dependencies
    compile "net.bull.javamelody:javamelody-core:1.68.1"
    compile ("com.lowagie:itext:2.1.7") {
        exclude module: "bcmail-jdk14"
        exclude module: "bcprov-jdk14"
        exclude module: "bctsp-jdk14"
    }
    compile "org.jrobin:jrobin:1.5.9"

}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
